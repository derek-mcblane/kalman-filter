include(ConvertBoolToYesNo)

set(DOXY_CONFIG_FILE_IN ${CMAKE_CURRENT_LIST_DIR}/Doxyfile.in CACHE FILEPATH "Doxygen configuration templated for use with CMake configure_file")

set(DOXY_USE_CLANG OFF CACHE BOOL
    "set CLANG_ASSISTED_PARSING = YES, and rely fully on compilation database for input paths")
convert_bool_to_yes_no(${DOXY_USE_CLANG} OUTPUT_VARIABLE DOXY_CLANG_ASSISTED_PARSING)
message(INFO ${DOXY_CLANG_ASSISTED_PARSING})
set(DOXY_CLANG_OPTIONS "" CACHE STRING
    "doxygen CLANG_OPTIONS setting")
set(DOXY_INPUT_DIRECTORY ${PROJECT_SOURCE_DIR}/source CACHE PATH
    "doxygen INPUT setting (overriden by DOXY_USE_CLANG)")
cmake_path(NATIVE_PATH DOXY_INPUT_DIRECTORY DOXY_INPUT)
set(DOXY_INPUT \"${DOXY_INPUT}\")
set(DOXY_EXCLUDE "" CACHE STRING
    "doxygen EXCLUDE setting")
set(DOXY_EXCLUDE_PATTERNS "" CACHE STRING
    "doxygen EXCLUDE_PATTERNS setting")

cmake_path(GET DOXY_CONFIG_FILE_IN FILENAME DOXY_CONFIG_FILE)
cmake_path(REMOVE_EXTENSION DOXY_CONFIG_FILE LAST_ONLY OUTPUT_VARIABLE DOXY_CONFIG_FILE)
set(DOXY_CONFIG_FILE ${CMAKE_CURRENT_BINARY_DIR}/${DOXY_CONFIG_FILE})

set(DOXY_CLANG_DATABASE_PATH $<$<BOOL:${DOXY_USE_CLANG}>:${CMAKE_BINARY_DIR}>)

add_custom_command(
    OUTPUT ${DOXY_CONFIG_FILE}
    COMMAND ${CMAKE_COMMAND}
        -DDOXY_CONFIG_FILE_IN=${DOXY_CONFIG_FILE_IN}
        -DDOXY_CONFIG_FILE=${DOXY_CONFIG_FILE}
        -DDOXY_PROJECT_NAME=${CMAKE_PROJECT_NAME}
        -DDOXY_INPUT=$<$<NOT:$<BOOL:${DOXY_USE_CLANG}>>:${DOXY_INPUT}>
        -DDOXY_EXCLUDE=${DOXY_EXCLUDE}
        -DDOXY_EXCLUDE_PATTERNS=${DOXY_EXCLUDE_PATTERNS} 
        -DDOXY_CLANG_ASSISTED_PARSING=${DOXY_CLANG_ASSISTED_PARSING}
        -DDOXY_CLANG_DATABASE_PATH=${CMAKE_BINARY_DIR}
        -DDOXY_CLANG_OPTIONS=${DOXY_CLANG_OPTIONS}
        -DGIT_COMMIT_ID_FILE=${GIT_COMMIT_ID_FILE}
        -P ${PROJECT_SOURCE_DIR}/scripts/ConfigureDoxygen.cmake
    DEPENDS ${DOXY_CONFIG_FILE_IN} ${GIT_COMMIT_ID_FILE} GitCommitIDFile
    COMMENT "Configuring ${DOXY_CONFIG_FILE} with CMake"
    VERBATIM
)

add_custom_target(doxygen
    COMMAND doxygen ${DOXY_CONFIG_FILE}
    DEPENDS  ${GIT_COMMIT_ID_FILE} GitCommitIDFile ${DOXY_CONFIG_FILE}
    COMMENT "Generating documentation with `doxygen ${DOXY_CONFIG_FILE}`"
    VERBATIM
)
